plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '2.26.0'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
}

group = 'net.cassite'
version = '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.compilerArgs += '--enable-preview'
    options.encoding = 'UTF-8'
}
tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}

application {
    mainModule = 'net.cassite.drawrgba'
    mainClass = 'net.cassite.drawrgba.Main'
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.swing', 'javafx.media']
}

dependencies {
    implementation group: 'io.vproxy', name: 'vjson', version: '1.5.4-jdk9'
    implementation group: 'io.vproxy', name: 'vfx', version: '1.3.5'
}

jlink {
    imageZip = project.file("${buildDir}/distributions/draw-rgba.zip")
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
        jvmArgs = ['--enable-preview', '--enable-native-access=ALL-UNNAMED']
        noConsole = true
    }
    mergedModule {
        additive = true
        forceMerge 'kotlin'
    }
}

jlinkZip {
    group = 'distribution'
}
